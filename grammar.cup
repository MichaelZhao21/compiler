import java_cup.runtime.*;

terminal    ASSIGN;
terminal    CLASS;
terminal    FINAL;
terminal    INT;
terminal    BOOL;
terminal    CHAR;
terminal    FLOAT;
terminal    VOID;
terminal    TRUE;
terminal    FALSE;
terminal    IF;
terminal    ELSE;
terminal    WHILE;
terminal    READ;
terminal    PRINTLINE;
terminal    PRINT;
terminal    RETURN;
terminal    INCREMENT;
terminal    DECREMENT;
terminal    MULTIPLY;
terminal    DIVIDE;
terminal    ADD;
terminal    SUBTRACT;
terminal    LT;
terminal    GT;
terminal    LTE;
terminal    GTE;
terminal    EQ;
terminal    NE;
terminal    OR;
terminal    AND;
terminal    LPAREN;
terminal    RPAREN;
terminal    LBRACKET;
terminal    RBRACKET;
terminal    LCURLY;
terminal    RCURLY;
terminal    COMPLEMENT;
terminal    QUESTION;
terminal    COLON;
terminal    COMMA;
terminal    PREFIXPLUS;
terminal    PREFIXMINUS;
terminal    TYPECAST;
terminal    TERNARY;

terminal boolean    SEMI;
terminal int        INTEGER_LITERAL;
terminal float      FLOAT_LITERAL;
terminal String     CHAR_LITERAL;
terminal String     STRING_LITERAL;
terminal String     ID;

non terminal Program            program;
non terminal DeclerationList    memberDeclerations;
non terminal FieldList          fieldList;
non terminal Field              fieldDecleration;
non terminal DeclerationList    methodList;
non terminal Method             methodDecleration;
non terminal StatementList      statements;
non terminal Statement          statement;
non terminal Statement          ifStatement;
non terminal Statement          whileStatement;
non terminal Statement          returnStatement;
non terminal Statement          assignStatement;
non terminal Statement          functionStatement;
non terminal Statement          callStatement;
non terminal Statement          suffixStatement;
non terminal Statement          scopeStatement;
non terminal ElseClause         elseClause;
non terminal Expression         expression; 
non terminal Expression         binaryExpression;
non terminal Expression         unaryExpression;
non terminal Expression         operandExpression;
non terminal Expression         callExpression;
non terminal FunctionList       argumentList;
non terminal FunctionList       readList;
non terminal FunctionList       printList;
non terminal FunctionList       printLineList;
non terminal FunctionList       parameterList;
non terminal FunctionList       parameterSublist;
non terminal Parameter          parameter;
non terminal Name               name;
non terminal Type               type;
non terminal Type               returnType;
non terminal Boolean            optionalSemi;

precedence right QUESTION, COLON, TERNARY;
precedence left OR;
precedence left AND;
precedence left NE, EQ;
precedence left LT, GT, LTE, GTE;
precedence left ADD, SUBTRACT;
precedence left MULTIPLY, DIVIDE;
precedence left TYPECAST;
precedence right PREFIXPLUS, PREFIXMINUS, COMPLEMENT, INCREMENT, DECREMENT;

program             ::= CLASS ID:i LCURLY memberDeclerations:m RCURLY
                        {: RESULT = new Program(new Name(i), m); :}
                        ;

memberDeclerations  ::= fieldDecleration:f memberDeclerations:m
                        {: RESULT = m.prepend(f); :}
                        |
                        methodDecleration:d methodList:m
                        {: RESULT = m.prepend(d); :}
                        |
                        {: RESULT = new DeclerationList(); :}
                        ;

fieldList           ::= fieldDecleration:f fieldList:ff
                        {: RESULT = ff.prepend(f); :}
                        |
                        {: RESULT = new FieldList(); :}
                        ;

fieldDecleration    ::= FINAL type:t ID:i ASSIGN expression:e SEMI
                        {: RESULT = new Field(t, new Name(i), e, true); :}
                        |
                        type:t ID:i ASSIGN expression:e SEMI
                        {: RESULT = new Field(t, new Name(i), e, false); :}
                        |
                        FINAL type:t ID:i SEMI
                        {: RESULT = new Field(t, new Name(i), true); :}
                        |
                        type:t ID:i SEMI
                        {: RESULT = new Field(t, new Name(i), false); :}
                        |
                        type:t ID:i LBRACKET INTEGER_LITERAL:in RBRACKET SEMI
                        {: RESULT = new Field(t, new Name(i, in), false); :}
                        ;

methodList          ::= methodDecleration:m methodList:mm
                        {: RESULT = mm.prepend(m); :}
                        |
                        {: RESULT = new DeclerationList(); :}
                        ;

methodDecleration   ::= type:t ID:i LPAREN parameterList:p RPAREN LCURLY fieldList:f statements:s RCURLY optionalSemi:os 
                        {: RESULT = new Method(t, new Name(i), p, f, s, os); :}
                        |
                        VOID ID:i LPAREN parameterList:p RPAREN LCURLY fieldList:f statements:s RCURLY optionalSemi:os 
                        {: RESULT = new Method(new Type("void"), new Name(i), p, f, s, os); :}
                        ;

parameterList       ::= parameter:p parameterSublist:pl
                        {: RESULT = pl.prepend(p); :}
                        |
                        {: RESULT = new ParameterList(); :}
                        ;

parameterSublist    ::= COMMA parameter:p parameterSublist:pl
                        {: RESULT = pl.prepend(p); :}
                        |
                        {: RESULT = new ParameterList(); :}
                        ;

parameter           ::= type:t ID:i
                        {: RESULT = new Parameter(t, new Name(i), false); :}
                        |
                        type:t ID:i LBRACKET RBRACKET
                        {: RESULT = new Parameter(t, new Name(i), true); :}
                        ;

statements          ::= statement:s statements:ss
                        {: RESULT = ss.prepend(s); :}
                        |
                        {: RESULT = new StatementList(); :}
                        ;

statement           ::= ifStatement:i
                        {: RESULT = i; :}
                        |
                        whileStatement:w
                        {: RESULT = w; :}
                        |
                        returnStatement:r
                        {: RESULT = r; :}
                        |
                        assignStatement:a
                        {: RESULT = a; :}
                        |
                        functionStatement:f
                        {: RESULT = f; :}
                        |
                        callStatement:c
                        {: RESULT = c; :}
                        |
                        suffixStatement:s
                        {: RESULT = s; :}
                        |
                        scopeStatement:ss
                        {: RESULT = ss; :}
                        ;

ifStatement         ::= IF LPAREN expression:e RPAREN LCURLY fieldList:f statements:s RCURLY elseClause:ec
                        {: RESULT = new IfStatement(e, f, s, ec); :}
                        ;

whileStatement      ::= WHILE LPAREN expression:e RPAREN LCURLY fieldList:f statements:s RCURLY
                        {: RESULT = new WhileStatement(e, f, s); :}
                        ;

returnStatement     ::= RETURN expression:e SEMI
                        {: RESULT = new ReturnStatement(e); :}
                        |
                        RETURN SEMI
                        {: RESULT = new ReturnStatement(); :}
                        ;

assignStatement     ::= name:n ASSIGN expression:e SEMI
                        {: RESULT = new AssignmentStatement(n, e); :}
                        ;

functionStatement   ::= READ LPAREN readList:r RPAREN SEMI
                        {: RESULT = new LibraryFunctionStatement("read", r); :}
                        |
                        PRINT LPAREN printList:p RPAREN SEMI
                        {: RESULT = new LibraryFunctionStatement("print", p); :}
                        |
                        PRINTLINE LPAREN printLineList:p RPAREN SEMI
                        {: RESULT = new LibraryFunctionStatement("printline", p); :}
                        ;

callStatement       ::= callExpression:c SEMI
                        {: RESULT = new CallStatement(c); :}
                        ;

suffixStatement     ::= name:n INCREMENT SEMI
                        {: RESULT = new SuffixStatement(n, "++"); :}
                        |
                        name:n DECREMENT SEMI
                        {: RESULT = new SuffixStatement(n, "--"); :}
                        ;

scopeStatement      ::= LCURLY fieldList:f statements:s RCURLY optionalSemi:os
                        {: RESULT = new ScopeStatement(f, s, os); :}
                        ;

argumentList        ::= expression:e COMMA argumentList:a
                        {: RESULT = a.prepend(e); :}
                        |
                        expression:e
                        {: RESULT = new ArgumentList(e); :}
                        ;

printList           ::= expression:e COMMA printList:p
                        {: RESULT = p.prepend(e); :}
                        |
                        expression:e
                        {: RESULT = new PrintList(e); :}
                        ;

printLineList       ::= printList:p
                        {: RESULT = p; :}
                        |
                        {: RESULT = new PrintList(); :}
                        ;

readList            ::= name:n COMMA readList:r
                        {: RESULT = r.prepend(n); :}
                        |
                        name:n
                        {: RESULT = new ReadList(n); :}
                        ;

elseClause          ::= ELSE LCURLY fieldList:f statements:s RCURLY
                        {: RESULT = new ElseClause(f, s); :}
                        |
                        {: RESULT = new ElseClause(); :}
                        ;
                        
type                ::= INT
                        {: RESULT = new Type("int"); :}
                        |
                        CHAR
                        {: RESULT = new Type("char"); :}
                        |
                        BOOL
                        {: RESULT = new Type("boolean"); :}
                        |
                        FLOAT
                        {: RESULT = new Type("float"); :}
                        ;

expression          ::= binaryExpression:b
                        {: RESULT = b; :}
                        |
                        unaryExpression:u
                        {: RESULT = u; :}
                        |
                        name:n
                        {: RESULT = n; :}
                        |
                        operandExpression:o
                        {: RESULT = o; :}
                        |
                        callExpression:c
                        {: RESULT = c; :}
                        |
                        LPAREN expression:e RPAREN
                        {: RESULT = new ParenExpression(e); :}
                        |
                        LPAREN type:t RPAREN expression:e
                        {: RESULT = new CastExpression(t, e); :}
                        %prec TYPECAST
                        |
                        LPAREN expression:e1 QUESTION expression:e2 COLON expression:e3 RPAREN
                        {: RESULT = new TernaryExpression(e1, e2, e3); :}
                        ;

binaryExpression    ::= expression:e1 ADD expression:e2
                        {: RESULT = new BinaryExpression(e1, "+", e2); :}
                        |
                        expression:e1 SUBTRACT expression:e2
                        {: RESULT = new BinaryExpression(e1, "-", e2); :}
                        |
                        expression:e1 MULTIPLY expression:e2
                        {: RESULT = new BinaryExpression(e1, "*", e2); :}
                        |
                        expression:e1 DIVIDE expression:e2
                        {: RESULT = new BinaryExpression(e1, "/", e2); :}
                        |
                        expression:e1 LT expression:e2
                        {: RESULT = new BinaryExpression(e1, "<", e2); :}
                        |
                        expression:e1 GT expression:e2
                        {: RESULT = new BinaryExpression(e1, ">", e2); :}
                        |
                        expression:e1 LTE expression:e2
                        {: RESULT = new BinaryExpression(e1, "<=", e2); :}
                        |
                        expression:e1 GTE expression:e2
                        {: RESULT = new BinaryExpression(e1, ">=", e2); :}
                        |
                        expression:e1 EQ expression:e2
                        {: RESULT = new BinaryExpression(e1, "==", e2); :}
                        |
                        expression:e1 NE expression:e2
                        {: RESULT = new BinaryExpression(e1, "<>", e2); :}
                        |
                        expression:e1 OR expression:e2
                        {: RESULT = new BinaryExpression(e1, "||", e2); :}
                        |
                        expression:e1 AND expression:e2
                        {: RESULT = new BinaryExpression(e1, "&&", e2); :}
                        ;

unaryExpression     ::= COMPLEMENT expression:e
                        {: RESULT = new UnaryExpression("~", e); :}
                        |
                        ADD expression:e
                        {: RESULT = new UnaryExpression("+", e); :}
                        %prec PREFIXPLUS
                        |
                        SUBTRACT expression:e
                        {: RESULT = new UnaryExpression("-", e); :}
                        %prec PREFIXMINUS
                        ;

name                    ::= ID:i
                        {: RESULT = new Name(i); :}
                        |
                        ID:i LBRACKET expression:e RBRACKET
                        {: RESULT = new Name(i, e); :}
                        ;

operandExpression   ::= INTEGER_LITERAL:i
                        {: RESULT = new OperandExpression(i); :}
                        |
                        FLOAT_LITERAL:f
                        {: RESULT = new OperandExpression(f); :}
                        |
                        CHAR_LITERAL:c
                        {: RESULT = new OperandExpression(c, "char"); :}
                        |
                        STRING_LITERAL:s
                        {: RESULT = new OperandExpression(s, "string"); :}
                        |
                        TRUE
                        {: RESULT = new OperandExpression("true", "boolean"); :}
                        |
                        FALSE
                        {: RESULT = new OperandExpression("false", "boolean"); :}
                        ;

callExpression      ::= ID:i LPAREN RPAREN
                        {: RESULT = new CallExpression(i); :}
                        |
                        ID:i LPAREN argumentList:a RPAREN
                        {: RESULT = new CallExpression(i, a); :}
                        ;

optionalSemi        ::= SEMI
                        {: RESULT = true; :}
                        |
                        {: RESULT = false; :}
                        ;
